import java.text.SimpleDateFormat

String swtBase = '../libraries/swt/';
String basicsBase = '../libraries/basics-1.0.m3/';

project.ext.lwjglVersion = "3.0.0b"
project.ext.jomlVersion = "1.7.1"

List<String> classPathExtension = []
List<String> nativeLibs = []
switch(System.getProperty('os.name').toLowerCase().split()[0]) {
    case 'windows':
        classPathExtension += [swtBase + 'swt-4.5-win32-win32-x86_64/swt-debug.jar']
        nativeLibs += "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-windows"
        break

    case 'linux':
        classPathExtension += [swtBase + 'swt-4.5-gtk-linux-x86_64/swt-debug.jar']
        nativeLibs += "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-linux"
        break

    case 'mac':
        classPathExtension += [swtBase + 'swt-4.5-cocoa-macosx-x86_64/swt-debug.jar']
        classPathExtension += [basicsBase + 'basics-ui-cocoa.jar']
        nativeLibs += "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-osx"
        break

    default:
        throw new Exception('Unknown OS')
}



dependencies {
    compile project(':core')

    compile files(basicsBase + 'basics.jar')
    compile files(basicsBase + 'basics-ui.jar')

    compile files(classPathExtension.toArray())

    compile group: 'org.jocl', name: 'jocl', version: '0.1.9'

    compile "org.lwjgl:lwjgl:${lwjglVersion}"

    for (String lib : nativeLibs) {
        compile lib
    }

    compile "org.joml:joml:${jomlVersion}"
}

task (writeVersion) {
    doLast {
        println("Writing version " + version)
        File versionFile = file('src/main/resources' + '/matrixstudio/ui/version.properties')

        Properties properties = new Properties()
        properties.put("version", version)
        properties.put("date", new SimpleDateFormat('MMMM d\'th\' yyyy', Locale.ENGLISH).format(new Date()))
        properties.put("daily", Boolean.toString(project.ext.daily))
        properties.store(new FileOutputStream(versionFile), "Generated by gradle task")
    }

}

processResources.dependsOn writeVersion
