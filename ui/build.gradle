import java.text.SimpleDateFormat

project.ext.lwjglVersion = "3.0.0b"
project.ext.jomlVersion = "1.7.1"

def allDependenciesForPlatform(String platform) {
    def dependencies = []

    dependencies += project(':core')

    dependencies += ':basics'
    dependencies += ':basics-ui'
    dependencies += 'org.jocl:jocl:0.1.9'

    dependencies += "org.lwjgl:lwjgl:${lwjglVersion}"
    dependencies += "org.joml:joml:${jomlVersion}"

    dependencies += ":swt-${platform}"
    switch(platform) {
        case 'windows':
            dependencies += "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-windows"
            break

        case 'linux':
            dependencies += "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-linux"
            break

        case 'mac':
            dependencies += ':basics-ui-cocoa'
            dependencies += "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-osx"
            break

        default:
            throw new Exception('Unknown OS')
    }

    return dependencies
}

dependencies {
    // to add new dependencies for compile add them in allDependenciesForPlatform method
    def platform = System.getProperty('os.name').toLowerCase().split()[0]
    for (def dependency : allDependenciesForPlatform(platform)) {
        compile dependency
    }
}

task (writeVersion) {
    doLast {
        File versionFile = file('src/main/resources' + '/matrixstudio/ui/version.properties')
        Properties properties = new Properties()
        properties.put("version", version)
        properties.put("date", new SimpleDateFormat('MMMM d\'th\' yyyy', Locale.ENGLISH).format(new Date()))
        properties.put("daily", Boolean.toString(project.ext.daily))
        properties.store(new FileOutputStream(versionFile), "Generated by gradle task")
    }
}

processResources.dependsOn writeVersion

def rootTemplateDir = new File("template");
def destDir = new File(buildDir, "distribution");

def resolvedDependencies(String platform) {
    def configuration = configurations.create("configuration-${platform}")
    for (def dependency : allDependenciesForPlatform(platform)) {
        configuration.dependencies.add(dependencies.create(dependency))
    }

    def result = configuration.resolve()

    // adds this project
    result.add(jar.archivePath)

    return result;
}

task macDistribution(type: Tar, dependsOn: 'assemble') {
    def platform = "mac"

    def templateDir = new File(rootTemplateDir, platform)
    def executable = "MatrixStudio.app/Contents/MacOS/MatrixStudio"
    def jarPath = "MatrixStudio.app/Contents/MacOS/jars"

    into (".") {
        from fileTree(templateDir) { exclude(executable) }
        from fileTree(templateDir) {
            include(executable)
            fileMode = 0755
        }
    }

    into (jarPath) { from resolvedDependencies(platform)}

    baseName = "MatrixStudio-${project.ext.daily ? 'daily' : "release"}-${platform}"
    destinationDir = destDir
    extension = 'tar.gz'
    compression = Compression.GZIP
}

task linuxDistribution(type: Tar, dependsOn: 'assemble') {
    def platform = "linux"

    def templateDir = new File(rootTemplateDir, platform)
    def executable = "MatrixStudio/MatrixStudio"
    def jarPath = "MatrixStudio/jars"

    into (".") {
        from fileTree(templateDir) { exclude(executable) }
        from fileTree(templateDir) {
            include(executable)
            fileMode = 0755
        }
    }

    into (jarPath) { from resolvedDependencies(platform)}

    baseName = "MatrixStudio-${project.ext.daily ? 'daily' : "release"}-${platform}"
    destinationDir = destDir
    extension = 'tar.gz'
    compression = Compression.GZIP
}

task windowsDistribution(type: Zip, dependsOn: 'assemble') {
    def platform = "windows"

    def templateDir = new File(rootTemplateDir, platform)
    def jarPath = "MatrixStudio/jars"

    into (".") { from fileTree(templateDir) }
    into (jarPath) { from resolvedDependencies(platform)}

    baseName = "MatrixStudio-${project.ext.daily ? 'daily' : "release"}-${platform}"
    destinationDir = destDir
}

task distribution (dependsOn: ['macDistribution','linuxDistribution', "windowsDistribution"])

apply plugin: 'com.jfrog.bintray'

bintrayUpload.dependsOn distribution

String versionName = project.ext.daily ? 'daily' : version
bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    filesSpec {
        from files(new File(buildDir, "distribution")) { include("/MatrixStudio-*") }
        into "matrixstudio/${versionName}"
    }
    publish = true
    override = true

    pkg {
        repo = 'generic'
        name = 'matrixstudio'
        userOrg = 'jeancharles-roger'
        githubRepo = 'jeancharles-roger/matrixstudio'
        githubReleaseNotesFile = "ChangeLog.md"

        version {
            name = versionName
        }
    }
}
