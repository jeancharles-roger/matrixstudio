allprojects {
    apply plugin: 'java'

    ext.daily = true

    sourceCompatibility = 1.7

    group 'org.matrixstudio'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
    }
}

dependencies {
    compile project(':core')
    compile project(':ui')
}

task macosDistribution(dependsOn: 'assemble') {
    doLast {
        println("Build distribution (daily:" + daily + ")");

        File templateDir = new File("template");

        File destDir = new File(buildDir, "distribution");
        delete(destDir);
        destDir.mkdir();


        // Prepares platform dependent directories
        File macosDir = new File(destDir, "macos");
        File macosJarDir = new File(macosDir, "MatrixStudio.app/Contents/MacOS/jars")
        copy {
            from new File(templateDir, "macos")
            into macosDir
        }
        configurations.compile.each { lib ->
            println("Copying ${lib.name}")
            copy {
                from lib.parent
                include lib.name
                into macosJarDir
            }
        }
    }
}

task macosTar(type: Tar, dependsOn: macosDistribution) {
    from files { new File(buildDir, "distribution/macos") }
    baseName = "MatrixStudio-${project.ext.daily ? 'daily' : project.version}-macos"
    destinationDir = new File(buildDir, "distribution")
    extension = 'tar.gz'
    compression = Compression.GZIP
}

task distribution (dependsOn: 'macosTar') {
    doLast {
        println("Distribution done")
    }
}
